// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/02/ALU.hdl
/**
 * ALU (Arithmetic Logic Unit):
 * Computes out = one of the following functions:
 *                0, 1, -1,
 *                x, y, !x, !y, -x, -y,
 *                x + 1, y + 1, x - 1, y - 1,
 *                x + y, x - y, y - x,
 *                x & y, x | y
 * on the 16-bit inputs x, y,
 * according to the input bits zx, nx, zy, ny, f, no.
 * In addition, computes the two output bits:
 * if (out == 0) zr = 1, else zr = 0
 * if (out < 0)  ng = 1, else ng = 0
 */
// Implementation: Manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) sets x = 0        // 16-bit constant
// if (nx == 1) sets x = !x       // bitwise not
// if (zy == 1) sets y = 0        // 16-bit constant
// if (ny == 1) sets y = !y       // bitwise not
// if (f == 1)  sets out = x + y  // integer 2's complement addition
// if (f == 0)  sets out = x & y  // bitwise and
// if (no == 1) sets out = !out   // bitwise not

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute (out = x + y) or (out = x & y)?
        no; // negate the out output?
    OUT 
        out[16], // 16-bit output
        zr,      // if (out == 0) equals 1, else 0
        ng;      // if (out < 0)  equals 1, else 0

    PARTS:
    
    // x 
    Mux16(a = x, b = false, sel = zx, out = s1X);
    Not16(in = s1X, out = notX);
    Mux16(a = s1X, b = notX, sel = nx, out = s2X);

    // y 
    Mux16(a = y, b = false, sel = zy, out = s1Y);
    Not16(in = s1Y, out = notY);
    Mux16(a = s1Y, b = notY, sel = ny, out = s2Y);

    // select + or &
    Add16(a = s2X, b = s2Y, out = addOut);
    And16(a = s2X, b = s2Y, out = andOut);
    Mux16(a = andOut, b = addOut, sel = f, out = prefinal);

    Not16(in = prefinal, out = notPrefinal);
    Mux16(a = prefinal, b = notPrefinal, sel = no, out = final);
    
    And16(a = final, b = true, out[0..7] = zr1, out[8..15] = zr2, out[15] = ng);
    Or8Way(in = zr1, out = zr3);
    Or8Way(in = zr2, out = zr4);
    Or(a = zr3, b = zr4, out = notZr);
    Not(in = notZr, out = zr);
    And16(a = final, b = true, out = out);
}


